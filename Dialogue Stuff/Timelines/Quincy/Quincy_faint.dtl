Dalton (Sigh): ...
Dalton (Resting): ...?
Dalton (Sigh): What happened?
Theo (Talk): You passed out after you took a sip of your drink.
Theo (Silly): If you can't drink, you should've just said so, you old fox.
Dalton (Sigh): Mmmm.
Dalton (Resting): How long was I out?
Theo (Thinking): Around 4 hours.
Dalton (Surprised): What?!
if {Juniper.found_skylar} == true:
	Theo (Surprised): Hey!
	Theo (Concerned): Chill!
	Theo (Laugh): I think I figured it out.
	Dalton (Squint): The case?
	Theo (Silly): Yeah.
	Theo (Happy): Did you know that Skylar Aslan is Quincy's son?
	Dalton (Doubt1): What?
	Theo (Thinking): Yeah, they aren't close, so Skylar took his mother's last name.
	Dalton (Resting): Hmph.
	Dalton (Awkward): I see.
	Theo (Talk): I reported back to the station.
	Theo (Silly): They dispatched a unit to go search for Skylar.
	Theo (Happy): Hopefully we’ll be able to question him tomorrow.
	Dalton (Awkward): I see.
	Quincy (Sigh): If you're still not feeling well, you can stay here for a bit longer.
	Dalton (Awkward): Ah, no.
	Dalton (Resting): It's fine.
	Dalton (Talk): We should get back to the station.
	Theo (Concerned): Are you sure you can drive?
	Theo (Awkward): I can drive if you need me to.
	Dalton (Sigh): It's fine.
	Dalton (Resting): I sobered up with the nap.
	Theo (Concerned): Okay.
	Quincy (Awkward): Ah...
	Quincy (Sigh): I wish we had more time to talk, but I got to head to a meeting.
	Quincy (Smug): Let's go for dinner after you both close this case.
	Theo (Laugh): For sure!
	Theo (Default): We'll make sure to knock some sense into that son of yours once we bring him in.
	Quincy (Squint Smug): As you should.
	Quincy (Sigh): Thank you for continuing to bring peace, safety, and justice to Arcadia Springs.
	Theo (Laugh): Heh...
	Theo (Silly): It is our job!
	Theo (Happy): Well, we’re gonna head out now!
	Theo (Laugh): Bye!
	Quincy (Happy): Goodbye!
	Dalton (Sigh): Bye.
	do GlobalVars.emit_add_note("quincy", "faint", "skylar")
	[end_timeline]
else:
	Theo (Awkward): Well...
	Theo (Talk): I looked around while you were asleep and I found a few things.
	Theo (Silly): All damage seems to have been done by hand or with a blunt object.
	Theo (Awkward): I wasn't able to crack the case, but I shook it a little...
	Theo (Default): And it sounds like there's a piece of paper and a something metal inside of it.
	Dalton (Resting): And this helps us how?
	Theo (Thinking): ...
	Theo (Sad): I don't know...
	if {Quincy.asked_Micah} == false or {Quincy.asked_Juniper} == false:
		Dalton (Sigh): Does Quincy even know who the other victims are?
		Theo (Awkward): No...
		Theo (Sad): He doesn't.
	Dalton (Sigh): Huuu...
	Dalton (Awkward): We should head back, it's late.
	Theo (Awkward): Yeah...
	Dalton (Squint): Where's Quincy?
	Theo (Thinking): Uh....
	Theo (Talk): He left.
	Theo (Awkward): Mayor business, I guess.
	Dalton (Sigh): Hmph.
	Dalton (Resting): Let's go.
	if {Quincy.Quincy_asked_case} == true:
		do GlobalVars.emit_add_note("quincy", "faint", "asked_case")
	else:
		do GlobalVars.emit_add_note("quincy", "faint", "no_asked_case")
	[signal arg="follow_dalton"]
	[end_timeline]

if {Character Aff Points.Juniper} < 0:
	set {Juniper.saw_pie} = true
	Dalton (Resting): Did you take a bite out of this pastry?
	Juniper (Doubt): Hm?
	Juniper (Awkward): What pastry?
	Juniper (Hmph): Oh, that one.
	Juniper: Uh...
	Juniper (Awkward): Yeah, I was in a rush yesterday.
	Dalton (Doubt1): ...
	Dalton (Talk): I see.
	do GlobalVars.emit_add_note("juniper", "pastry", "low")
else:
	set {Juniper.saw_pie} = true
	Dalton (Talk): I found some damage. 
	Juniper (Surprise): What?!
	Dalton (Silly): Someone took a bite out of your pastry. 
	Juniper (Surprise): Bah!
	Juniper (Default): Why did you have to scare me like that!?
	Theo (Laugh): That old fox got jokes sometimes.
	Theo (Concerned): It’s scary when he does.
	Juniper (Sigh): Huu...
	Juniper (Roll Eyes): Well, I hope they got their money's worth. 
	Juniper (Hmph): That pastry’s been out for days.
	do GlobalVars.emit_add_note("juniper", "pastry", "high")
	- Take as evidence.
		Dalton (Talk):  Can I take the pastry in for examination?
		Juniper (Happy): Oh! 
		Juniper (Default): Of course!
		Juniper (Hmph): I was just gonna throw it anyway.
		Dalton (Resting): Hmph.
		Dalton (Awkward): Thanks.
		set {Juniper.has_pie} = true
		do GlobalVars.emit_add_evidence("juniper", "pie")
	- Leave it.
		Dalton (Resting): Hmph.

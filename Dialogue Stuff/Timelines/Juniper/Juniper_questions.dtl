set {Juniper.num_questions_asked} = 0
if {Character Aff Points.Juniper} < 0:
	Dalton (Resting): Can I ask you a few questions?
	Juniper (Sigh): Sure...
	Juniper (Squint): Make it quick.
else:
	Dalton (Talk): Can I ask you a few questions?
	Juniper (Happy): Of course!
	Juniper (Default): Ask away!

label questions
[wait time="0.3"]
Dalton (Default): Ask about the...
- Break-in.
	[wait time="0.3"]
	Dalton: Ask about...
	- Location.
		set {Juniper.num_questions_asked} += 1
		jump Juniper_questions_time/
	- Damage.
		set {Juniper.num_questions_asked} += 1
		jump Juniper_questions_missing/
	- Motive.
		set {Juniper.num_questions_asked} += 1
		jump Juniper_questions_who/
	- Go back
		jump questions
- Other victims.
	[wait time="0.3"]
	Dalton (Default): Show picture of...
	- Micah
		set {Juniper.num_questions_asked} += 1
		jump Juniper_questions_Micah/
	- Quincy
		set {Juniper.num_questions_asked} += 1
		jump Juniper_questions_quincy/
	- Go back
		jump questions
- Continue.
	if {Juniper.num_questions_asked} == 0:
		Dalton (Awkward): Um...
		Dalton (Resting): Nevermind.
		if {Character Aff Points.Juniper} < 0:
			Juniper (Squint): Tsk.
		else:
			Juniper (Default): heh.
			Juniper (Happy): Okay.
	else:
		Dalton (Resting): That's all the questions I have right now.
	[end_timeline]
